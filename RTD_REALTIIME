#include <ESP8266WiFi.h>
#include <FirebaseESP8266.h>
#include <ArduinoJson.h>
#include <Adafruit_MAX31865.h>
#include <TimeLib.h>
#include <WiFiUdp.h>
#include <ESP8266HTTPClient.h>
#include <WiFiClientSecure.h>

// WiFi Configuration
const char* WIFI_SSID = "Tan_Pocket";
const char* WIFI_PASSWORD = "15932456";

// Firebase Configuration
const char* DATABASE_URL = "https://tvo-op3-default-rtdb.firebaseio.com/";
const char* DATABASE_SECRET = "vJl5LpxP4O6vcox76p2PFknSgwDG4O5cEs8yLTLC";

// Power BI Configuration
const char* powerbiUrl = "https://api.powerbi.com/beta/9bc585f9-a8b8-431c-9013-efa7b2b40cdf/datasets/2d0edd16-9f4b-4eac-a861-ef3607905b6a/rows?experience=power-bi&key=J3BDVfqJHNjxfbTtlBnm6x8P18oWRLpt3bLDzS6E3kQqA9dVIlR5SwDwUuhDOMvMHPksyu2KIriQYwpqpurSFg%3D%3D";

FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;
WiFiClientSecure secureClient;
WiFiClient client;
HTTPClient http;
WiFiUDP Udp;

// Time Configuration
const int timeZone = 7;     // UTC+7 Thailand
unsigned int localPort = 8888;
static const char ntpServerName[] = "pool.ntp.org";
const int NTP_PACKET_SIZE = 48;
byte packetBuffer[NTP_PACKET_SIZE];
String datetime;

// MAX31865 configuration (SPI)
#define MAX_CS   D3  // Chip Select (CS)
#define MAX_CLK  D5  // Clock (SCK)
#define MAX_SDO  D6  // MISO
#define MAX_SDI  D7  // MOSI

// Initialize MAX31865
Adafruit_MAX31865 thermo = Adafruit_MAX31865(MAX_CS, MAX_SDI, MAX_SDO, MAX_CLK);

void sendNTPpacket(const char* address) {
 memset(packetBuffer, 0, NTP_PACKET_SIZE);
 packetBuffer[0] = 0b11100011;
 packetBuffer[1] = 0;
 packetBuffer[2] = 6;
 packetBuffer[3] = 0xEC;
 packetBuffer[12] = 49;
 packetBuffer[13] = 0x4E;
 packetBuffer[14] = 49;
 packetBuffer[15] = 52;
 Udp.beginPacket(address, 123);
 Udp.write(packetBuffer, NTP_PACKET_SIZE);
 Udp.endPacket();
}

time_t getNtpTime() {
 while (Udp.parsePacket() > 0) ; // discard any previously received packets
 Serial.println("Transmitting NTP Request");
 sendNTPpacket(ntpServerName);
 uint32_t beginWait = millis();
 while (millis() - beginWait < 1500) {
   int size = Udp.parsePacket();
   if (size >= NTP_PACKET_SIZE) {
     Serial.println("Received NTP Response");
     Udp.read(packetBuffer, NTP_PACKET_SIZE);
     unsigned long secsSince1900;
     secsSince1900 =  (unsigned long)packetBuffer[40] << 24;
     secsSince1900 |= (unsigned long)packetBuffer[41] << 16;
     secsSince1900 |= (unsigned long)packetBuffer[42] << 8;
     secsSince1900 |= (unsigned long)packetBuffer[43];
     return secsSince1900 - 2208988800UL + timeZone * SECS_PER_HOUR;
   }
 }
 Serial.println("No NTP Response");
 return 0;
}

void getDatetime() {
 char buffer[25];
 snprintf(buffer, sizeof(buffer), "%04d-%02d-%02d %02d:%02d:%02d", 
          year(), month(), day(), hour(), minute(), second());
 datetime = String(buffer);
}

void connectToWiFi() {
 WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
 Serial.print("Connecting to WiFi...");
 while (WiFi.status() != WL_CONNECTED) {
   delay(500);
   Serial.print(".");
 }
 Serial.println("\nConnected to WiFi!");
}

bool initFirebase() {
 config.database_url = DATABASE_URL;
 config.signer.tokens.legacy_token = DATABASE_SECRET;
 Firebase.begin(&config, &auth);
 Firebase.reconnectWiFi(true);
 
 if (Firebase.ready()) {
   Serial.println("Firebase connected successfully!");
   return true;
 } else {
   Serial.println("Failed to connect to Firebase!");
   return false;
 }
}

void sendToFirebase(float temperature, String datetime) {
 FirebaseJson json;
 json.set("temperature", temperature);
 
 // แทนที่ "UTC+07" ด้วย "" และ space ด้วย "_" สำหรับ path
 String pathTimestamp = datetime;
 pathTimestamp.replace(" UTC+07", "");
 pathTimestamp.replace(" ", "_");
 pathTimestamp.replace(":", "-");
 
 String path = "/sensor/" + pathTimestamp;
 
 if (Firebase.setJSON(fbdo, path, json)) {
   Serial.println("✅ Data sent successfully:");
   Serial.println("Path: " + path);
   Serial.println("Temperature: " + String(temperature));
   Serial.println("Timestamp: " + datetime);
 } else {
   Serial.print("❌ Error sending data: ");
   Serial.println(fbdo.errorReason());
 }
}

void sendToPowerBI(float temperature, String datetime) {
 http.begin(secureClient, powerbiUrl);
 http.addHeader("Content-Type", "application/json");
 String jsonData = "[{\"Temperature\": " + String(temperature, 2) + ", \"Datetime\": \"" + datetime + "\"}]";
 int httpCode = http.POST(jsonData);
 Serial.print("Power BI Response Code: ");
 Serial.println(httpCode);
 http.end();
}

void setup() {
 Serial.begin(115200);
 
 connectToWiFi();
 secureClient.setInsecure();
 
 Udp.begin(localPort);
 
 setSyncProvider(getNtpTime);
 setSyncInterval(300);
 
 Serial.println("Waiting for time sync");
 while (timeStatus() != timeSet) {
   Serial.print(".");
   delay(1000);
 }
 Serial.println("\nTime Synchronized!");
 
 if (!initFirebase()) {
   Serial.println("Failed to initialize Firebase. Restarting...");
   delay(1000);
   ESP.restart();
 }
 
 thermo.begin(MAX31865_3WIRE);
}

void loop() {
 if (WiFi.status() != WL_CONNECTED) {
   Serial.println("WiFi connection lost. Reconnecting...");
   connectToWiFi();
   return;
 }

 if (!Firebase.ready()) {
   Serial.println("Firebase connection lost. Reinitializing...");
   if (!initFirebase()) {
     Serial.println("Firebase reinitialization failed. Restarting...");
     delay(1000);
     ESP.restart();
   }
   return;
 }

 float temperature = thermo.temperature(100.0, 430.0);
 if (!isnan(temperature)) {
   getDatetime();
   Serial.print("Temperature: ");
   Serial.println(temperature);
   Serial.print("Datetime: ");
   Serial.println(datetime);
   
   sendToFirebase(temperature, datetime);
   sendToPowerBI(temperature, datetime);
 } else {
   Serial.println("Failed to read temperature!");
 }
 
 delay(5000);
}
